plugins {
  id 'java'
  id 'eclipse'
  id 'maven-publish'
  id 'signing'
  id 'com.palantir.git-version' version '3.0.0'
}

project.version '0.1.0'
group = 'io.github.agebe'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.jboss.resteasy:resteasy-core:6.2.8.Final'
  implementation 'com.google.inject:guice:7.0.0'
}

compileJava {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

publishing {
  repositories {
    maven {
      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = project.findProperty("sonatype.user") ?: ''
        password = project.findProperty("sonatype.password") ?: ''
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      pom {
        name = 'resteasy-guice'
        description = 'resteasy 6.x guice 7.x integration'
        url = 'https://github.com/agebe/resteasy-guice'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'agebe'
            name = 'Andre Gebers'
            email = 'andre.gebers@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/agebe/resteasy-guice.git'
          developerConnection = 'scm:git:ssh://github.com:agebe/resteasy-guice.git'
          url = 'https://github.com/agebe/resteasy-guice/tree/main'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.maven
}

java {
  withSourcesJar()
  withJavadocJar()
}

jar {
  manifest {
    attributes(
      'Implementation-Version': project.version,
      'Implementation-Title': project.name,
      'git-version': gitVersion())
  }
}

javadoc {
  options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api/']
}
